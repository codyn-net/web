<?xml version='1.0' encoding='utf-8'?>
<category id="Manual::Introduction::First Network" name="First Network" xmlns="http://jessevdk.github.com/cldoc/1.0">
  <brief>Writing your first network.</brief>
  <doc>If you've been reading the <ref ref="Manual::Introduction::Conceptual Overview#Manual::Introduction::Conceptual Overview">conceptual overview</ref>, then you have already
seen how a very simple example network can be defined. Here we will go
through constructing and simulating a network step by step so that you can
get an intuition of how you can use codyn to simulate dynamical networks.

## The system
Although networks can be created completely programmatically, the canonical and
convenient way to create a network is by using the codyn modeling language.
We will not go fully into the details of the language here, but provide a
short introduction into using its basic features. For more details on the
modeling language see <ref ref="Manual::Modeling#Manual::Modeling">modeling language</ref>.

The goal of this tutorial is to create the oscillator network as presented
in [From Swimming to Walking with a Salamander Robot Driven by a Spinal Cord Model (Ijspeert et al, 2007)][science2007]. In this article, a network of simple amplitude controlled phase
oscillator are used as an abstract representation of the salamander spinal
cord circuitry responsible for generating both swimming and walking.

![centered][scienceeq]

The equations above are used to model one oscillator in the
dynamical system of the salamander spinal cord (Ijspeert et al., 2007). Note
that each oscillator receives coupling on `θ` from its neighbors. Also,
the differential equation for `r` is second order and we will see how we
can implement this in codyn. In the article, a double chain of such oscillators
represent the full spinal chord. Although there are additional oscillators driving
the limbs and a mechanism to switch between swimming and walking, here we will
only model the swimming part of the system.

[science2007]: http://www.sciencemag.org/content/315/5817/1416.short
[scienceeq]: figures/scienceeq.png

## Base oscillator

Let's start by constructing the base amplitude controlled phase oscillator node.
We will model the phase and amplitude evolution of the oscillator, but not the
coupling term.

```cdn
templates
{
    node "oscillator"
    {
        ## The desired amplitude
        R = 1

        ## A positive constant
        a = 1

        ## The desired frequency
        ν = 1

        ## Randomized value for the phase
        θ = "rand(-π, π)"

        ## Phase state differential equation
        θ' = "2 ∙ π ∙ ν"

        ## Amplitude state differential equation
        r'' = "a ∙ (a / 4 ∙ (R - r) - r')"

        ## Oscillator output
        x = "r ∙ (1 + cos(θ))"
    }
}


```

Here we have defined an *oscillator* node in the templates section of the
network. The *oscillator* defines the variables `R`, `a` and `ν` wich
can be used to control the amplitude, rate of convergence and frequency of the
oscillator. It then defines two differential equations for the phase `θ`
and amplitude `r` using the *prime* syntax.

The *prime* syntax (e.g. `θ'`) can be used to define a state variable and
its corresponding differential equation on a single line. The initial condition
for the state variable will always be `0`, but can be defined separately to
a different value later. Furthermore, if you look closely at the differential
equation of `r` in the code above, you'll notice that we've used **two**
primes! This is a shorthand syntax to generate a second order differential
equation. Codyn internally only supports first order differential equations and
rewrites the order N equation as a cascade of first order equations. The
convenience syntax is equivalent to the following:

```cdn
 r' = "dr"
dr' = "a ∙ (a / 4 * (R - r) - dr)"
```

By using `r''`, the `dr` variable will be automatically added by codyn. Note
also that we can refer to the differential equation of `r` inside the
differential equation of `dr` by writing `r'`.

## Coupling
Next we will define the basic coupling between two oscillators as an edge
in the template section.

```cdn
templates
{
    edge "coupling"
    {
        ## Coupling weight
        w = "1"

        ## Coupling bias
        Φ = "0"

        ## Diffusive coupling term on the phase
        θ' += "r ∙ w ∙ sin(θ - output.θ - Φ)"
    }
}


```

The edge defines a single additive term (edges always define additive terms)
to the differential equation of the phase variable `θ'`. This type of coupling
is diffusive and can be seen as a forcing term towards a certain phase difference
between two oscillators, called the phase bias `Φ`. Note that there is a context
in which variables in equations are resolved. For an edge, a variable is first
looked for in the edge itself (for example `w`). If not found, the variable is
then looked for in the *input* node of the edge. Thus, in the above definition
of coupling differential equation for `θ`, `r` refers to the
amplitude state variable of the *input* node of the *coupling* edge. Finally,
you can also refer explicitly to variables in either *input* or *output* nodes
of an edge by using `input.variable` or `output.variable`. An example of this
is `output.θ` to access the phase state variable of the *output* node of the
coupling edge.

## Generating the nodes
Now that we have defined the `oscillator` and `coupling` templates, we can
continue generating the actual nodes that will represent our chain of oscillators.

```cdn
defines
{
    # Define the number of oscillators to generate, if not yet defined. This
    # define can afterwards be used anywhere else by writing @n.
    n ?= 8
}

# Generate @n oscillators for both right and left sides of the chain
node "oscillator_{1:@n}_{right,left}" : "oscillator"
{
}

# Set the (x, y) layout of the nodes so they appear as a chain when visualized
layout "oscillator_{1:@n}_{right,left}" at $(@2[!] ∙ 4), $(@1 ∙ 3)

```

There are a couple of new things that we used here to define the nodes. First of
all, we used something called `defines`. Defines are a language level type of
variable that you can use to parametrize the generation of your network. In
this case, we use a define to set the number of oscillators that we want
to generate. By doing this, we can later very easily change the number of
oscillators in the chain by only changing the value of `n`.
After having defined `n` we can refer to its value
anywhere in our network using `@n`. Note that we use
the special `?=` (instead of just `=`) to define the value of `n`. This means
that `n` will only be defined if it was not defined before. Doing so allows you
to include the above file somewhere else with a different defined `n`. For example:

```cdn
defines { n = 10 } include "first_network.cdn"
```

To generate the actual nodes representing
the oscillators, we use the generator syntax of the modeling language. This
syntax works very similar to brace expansion in `bash`. The braces generate
ranges of values which are expanded inside the string they occur in. For example
`"{a,b,c}"` would expand to `"a"`, `"b"` and `"c"`. Brace expansion is also
combinatorial, meaning that `"{a,b}_{c,d}"` would expand to `"a_c"`, `"a_d"`,
`"b_c"` and `"b_d"`. Additionally, `"n{1:3}"` would generate `"n1"`, `"n2"`
and `"n3"`. Using this syntax, we can easily generate the chain of oscillators
as done in the code shown above.

Finally, we use one line to position the generated oscillator nodes on a grid.
This type of layouting is only used for visualizing the network and has no
effect on the simulation. To layout all the nodes along a chain we use
&amp;lt;Manual::3. Modeling::Embedded calculations&amp;gt; and
&amp;lt;Manual::3. Modeling::Expansion references&amp;gt;.

Calculations
can be done inside the model specification using the `$(expression)` syntax.
The `expression` will be evaluated (after expanding references) and injected
at the point of the embedded calculation.

Expansion references can be used to refer to previously expanded expressions.
The syntax for these references is `@N` with `N` being a number. `@0` always
refers to the full expansion, while `@1`, `@2` etc. refer to sub-groups of
the expansion. In the case of brace expansions, `@1` would refer to the expansion
of the first occurence of braces. The special expansion reference `@N[!]` refers
to the *index* of expansion `N`. When refering to brace expansions, it's the
*index* of the expanded value. For example, the brace expansion `"n_{a,b}"` would
generate the following expansion references:

| Reference | value 1| value 2 |
|-----------|:------:|:-------:|
| `@0`      | n\_a   | n\_b    |
| `@1`      | a      | b       |
| `@1[!]`   | 0      | 1       |

## Generating the couplings

We have previously generated all the oscillator nodes, now we only need to
couple the phases of these oscillators such that they are producing a traveling
wave. We do this by introducing coupling edges between successive nodes on
each side of the chain (right and left). Furthermore, each pair of right/left
oscillators should oscillate in anti-phase. We therefore also introduce ipsilateral
coupling for each oscillator.

```cdn
## up/down coupling to generate a traveling wave
&lt;bidirectional&gt;
edge from "oscillator_{1:@n}_{right,left}"
       to "oscillator_$(@1 + 1)_@2" : "coupling"
{
    d = ["1", "-1"]
    Φ = "d ∙ 2 ∙ π / @n"
}

## left/right coupling to generate anti-phase coupling
&lt;bidirectional&gt;
edge from "oscillator_{1:@n}_right"
       to "oscillator_@1_left" : "coupling"
{
    d = ["1", "-1"]
    Φ = "d ∙ π"
}

```

Generating the edges is fairly straightforward. We make use of one new feature
here to automatically generate `bidirectional` coupling. This means that whenever
an edge is constructed with some `input` and `output`, another edge is constructed
automatically in the other way around. The phase coupling that we want to
introduce here should be symmetric. This basically means that we need to
negate the phase bias `Φ` in the bidirectional direction. To do this, we use
a feature called &amp;lt;Manual::3. Modeling::Multi expressions&amp;gt;.
Multi expressions allow you to specify different expressions for multiple objects
generated at the same level. The syntax is `["expression 1", "expression 2", "etc."]`.
This expression would evaluate to `"expression 1"` for the first generated object,
`"expression 2"` for the second generated object etc.

In our couplings we use this to set the expression of the `d` variable to
`1` and `-1` for respectively the up-to-down and down-to-up couplings (and
similarly for left-to-right and right-to-left).

## Generating the segments
The output of the chain of oscillators (for example joint angle) is actually
the subtraction of the output of two ipsilateral oscillators. To get this output
we will introduce a new segment node calculating this output. We first define
a new segment template as follows:

```cdn
templates
{
    node "segment"
    {
        right = "0"
        left = "0"

        x = "(right - left) / 2"
    }
}

```

This template simply has two variables for `left` and `right`, and an `x`
variable computing the difference of the two. Note the divisiion by 2 to scale
the output. Next we will create the segment nodes in the network and couple them
to the oscillators.

```cdn
node "segment_{1:@n}" : "segment" {}

# Segment coupling
edge from "oscillator_{1:@n}_{right,left}"
       to "segment_@1"
{
    @@2 &lt;= "x"
}

layout "segment_{1:@n}" at 2, $(@1 ∙ 3)

```

Here we have introduced one new thing. In codyn, edges are not restricted to
act on only state variables. If you want to *transmit* information from one
node to another through an edge, then you can do this using `x &lt;= expression` syntax in the
edge. This syntax basically assigns the value of `expression` to the variable `x`
in the `output` of the edge. We use this feature here to have access to the
`x` of the right and left oscillator of the edge in the segment node.

Note the use of `@@2` (two @'s) to reference an expansion in the second level of the
context stack. The value of `@@2` is thus respectively `right` and `left`,
referencing the second group in the expansion of `"oscillator_{1:@n}_{right,left}"`.

## Complete network
The following code shows the complete working network with annotations.

```cdn
templates
{
    node "oscillator"
    {
        ## The desired amplitude
        R = 1

        ## A positive constant
        a = 1

        ## The desired frequency
        ν = 1

        ## Randomized value for the phase
        θ = "rand(-π, π)"

        ## Phase state differential equation
        θ' = "2 ∙ π ∙ ν"

        ## Amplitude state differential equation
        r'' = "a ∙ (a / 4 ∙ (R - r) - r')"

        ## Oscillator output
        x = "r ∙ (1 + cos(θ))"
    }

    node "segment"
    {
        right = "0"
        left = "0"

        x = "(right - left) / 2"
    }

    edge "coupling"
    {
        ## Coupling weight
        w = "1"

        ## Coupling bias
        Φ = "0"

        ## Diffusive coupling term on the phase
        θ' += "r ∙ w ∙ sin(θ - output.θ - Φ)"
    }
}

defines
{
    # Define the number of oscillators to generate, if not yet defined. This
    # define can afterwards be used anywhere else by writing @n.
    n ?= 8
}

# Generate @n oscillators for both right and left sides of the chain
node "oscillator_{1:@n}_{right,left}" : "oscillator" {}
node "segment_{1:@n}"                 : "segment" {}

# Set the (x, y) layout of the nodes so they appear as a chain when visualized
layout "oscillator_{1:@n}_{right,left}" at $(@2[!] ∙ 4), $(@1 ∙ 3)
layout "segment_{1:@n}"                 at 2,            $(@1 ∙ 3)

# up/down coupling to generate a traveling wave
&lt;bidirectional&gt;
edge from "oscillator_{1:@n}_{right,left}"
       to "oscillator_$(@1 + 1)_@2" : "coupling"
{
    d = ["1", "-1"]
    Φ = "d ∙ 2 ∙ π / @n"
}

# left/right coupling to generate anti-phase coupling
&lt;bidirectional&gt;
edge from "oscillator_{1:@n}_right"
       to "oscillator_@1_left" : "coupling"
{
    d = ["1", "-1"]
    Φ = "d ∙ π"
}

# Segment coupling
edge from "oscillator_{1:@n}_{right,left}"
       to "segment_@1"
{
    @@2 &lt;= "x"
}

```

When visualized, this network looks like the following figure:

![centered][complete]

[complete]: figures/complete.png



</doc>
</category>